QEMU := "../../qemu-build/riscv64-softmmu/qemu-system-riscv64"
TARGET := "riscv64gc-unknown-none-elf"
MODE := "release"
OBJDUMP := "rust-objdump --arch-name=riscv64"
OBJCOPY := "rust-objcopy --binary-architecture=riscv64"

BUILD_PATH := "target/" + TARGET + "/" + MODE + "/"
KERNEL_ELF := BUILD_PATH + "os"
KERNEL_ASM := BUILD_PATH + "os.asm"
KERNEL_BIN := BUILD_PATH + "os.bin"

user:
    cd ../user && make build

build: user
    cp src/linker-qemu.ld src/linker.ld
    cargo build --features "board_qemu" --release
    {{OBJCOPY}} {{KERNEL_ELF}} --strip-all -O binary {{KERNEL_BIN}}
    rm src/linker.ld

disasm: build
    {{OBJDUMP}} -D -S {{KERNEL_ELF}} > {{KERNEL_ASM}}

run: build
    {{QEMU}} -machine virt -smp 4 -serial /dev/pts/2 -serial /dev/pts/3 -nographic -bios ./rustsbi-qemu.debug.bin -device loader,file={{KERNEL_BIN}},addr=0x80200000

debug_qemu: build
    {{QEMU}} -machine virt -smp 4 -serial /dev/pts/2 -serial /dev/pts/3 -nographic -bios ./rustsbi-qemu.bin -device loader,file={{KERNEL_BIN}},addr=0x80200000 -d int -D debug.log

debug: build disasm
    tmux new-session -d "{{QEMU}} -machine virt -smp 4 -serial /dev/pts/2 -serial /dev/pts/3 -nographic -bios ./rustsbi-qemu.debug.bin -device loader,file={{KERNEL_BIN}},addr=0x80200000 -s -S" && tmux split-window -h "riscv64-unknown-elf-gdb -ex 'file {{KERNEL_ELF}}' -ex 'set arch riscv:rv64' -ex 'target remote localhost:1234'" && tmux -2 attach-session -d

debug_all: build disasm
    tmux new-session -d "{{QEMU}} -machine virt -smp 4 -serial /dev/pts/1 -serial /dev/pts/2 -nographic -bios ./rustsbi-qemu.bin -device loader,file={{KERNEL_BIN}},addr=0x80200000 -d int -D debug.log -s -S" && tmux split-window -h "riscv64-unknown-elf-gdb -ex 'file {{KERNEL_ELF}}' -ex 'set arch riscv:rv64' -ex 'target remote localhost:1234'" && tmux -2 attach-session -d
